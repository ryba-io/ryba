
# Ambari Server Install

See the Ambari documentation relative to [Software Requirements][sr] before
executing this module.

    module.exports = header: 'Ambari Server Install', timeout: -1, handler: ->
      options = @config.ryba.ambari_server
      options.host = @config.host

## Identities

By default, the "ambari-server" package does not create any identities.

      @system.group header: 'Group', options.group
      @system.group header: 'Group Hadoop', options.hadoop_group
      @system.user header: 'User', options.user

## Package & Repository

Declare the Ambari custom repository.
Install Ambari server package.

      @file.download
        header: 'Repo'
        if: options.repo
        source: options.repo
        target: '/etc/yum.repos.d/ambari.repo'
      @system.execute
        cmd: "yum clean metadata; yum update -y"
        if: -> @status -1
      @service
        header: 'Package'
        name: 'ambari-server'
        startup: true
      @service
        header: 'Mysql Connector'
        name: 'mysql-connector-java'
        if: options.db.engine is 'mysql'

## Non-Root

      @file
        header: 'Sudo'
        if: options.sudo
        target: '/etc/sudoers.d/ambari_server'
        content: """
        # Ambari Commands
        ambari ALL=(ALL) NOPASSWD:SETENV: /bin/mkdir -p /etc/security/keytabs, /bin/chmod * /etc/security/keytabs/*.keytab, /bin/chown * /etc/security/keytabs/*.keytab, /bin/chgrp * /etc/security/keytabs/*.keytab, /bin/rm -f /etc/security/keytabs/*.keytab, /bin/cp -p -f /var/lib/ambari-server/data/tmp/* /etc/security/keytabs/*.keytab
        Defaults exempt_group = ambari
        Defaults !env_reset,env_delete-=PATH
        Defaults: ambari !requiretty
        """
      @system.remove
        header: 'Clean Sudo'
        unless: options.sudo
        target: '/etc/sudoers.d/ambari_server'

## Database

Prepare the Ambari Database

      @call header: 'Database', ->

Password is stored inside a file which location is referenced by the property
"server.jdbc.user.passwd" in the configuration file. The permissione "660" match
the ones generated by "ambari-server setup".

        @file
          header: 'Stash Password'
          unless: options.master_key
          target: options.config['server.jdbc.user.passwd']
          content: options.db.password
          backup: true
          mode: 0o0640
        # Note, for same ready, `ambari-server setup-security` keep re-generating
        # the stashed password file even if it uses the encrypted database located
        # in "/var/lib/ambari-sever/keys".
        # @system.remove
        #   header: 'Clean Stash Password'
        #   if: options.master_key
        #   target: '/etc/ambari-server/conf/password.dat'

Create the database hosting the Ambari data with restrictive user permissions.

        @db.user options.db, database: null,
          header: 'User'
          if: options.db.engine in ['mysql', 'postgres']
        @db.database options.db,
          header: 'Database'
          user: options.db.username
          if: options.db.engine in ['mysql', 'mariadb', 'postgres']
        @db.schema options.db,
          header: 'Schema'
          if: options.db.engine is 'postgres'
          schema: options.db.schema or options.db.database
          database: options.db.database
          owner: options.db.username

Load the database with initial data

        switch options.db.engine
          when 'mysql', 'mariadb'
            load = db.cmd(options.db, null) + '< /var/lib/ambari-server/resources/Ambari-DDL-MySQL-CREATE.sql'
            created = db.cmd(options.db, 'show tables') + '|  grep clusters'
          when 'postgres'
            load = db.cmd(options.db, null) + '< /var/lib/ambari-server/resources/Ambari-DDL-Postgres-CREATE.sql'
            created = db.cmd(options.db, null) + 'show tables |  grep clusters'
        @system.execute
          header: 'Init'
          cmd: load
          unless_exec: created

## Configuration

Merge used defined configuration. This could be used to set up 
LDAP or Active Directory Authentication. The permissions "644" are the ones 
generated by the "ambari-server setup" command.

      @file.properties
        header: 'Config'
        target: "#{options.conf_dir}/ambari.properties"
        content: options.config
        merge: true
        comment: true
        backup: true
        mode: 0o0644
    
## Setup

Password encryption is activated if the property "master_key" is configured. By 
default the passwords to access the Ambari database and the LDAP server are 
stored in a plain text configuration file. if Password encryption is activated, 
Ambari will store information inside "/var/lib/ambari-server/keys".

Be carefull, notes from Ambari 2.4.2:
* Options "jdbc-db" and "jdbc-driver" prevent the setup script from modifying 
  the properties file.
* Option "cluster-name" does nothing

      @call header: 'Setup', ->
        props = {}
        @call (options, callback) ->
          properties '/etc/ambari-server/conf/ambari.properties', ssh: options.ssh, (err, data) ->
            throw err if err
            for k, v of data
              props[k] ?= {}
              props[k].org = v 
            callback()
        @system.execute
          shy: true
          cmd: """
          ambari-server setup \
            -s \
            -j #{options.java_home} \
            --database=#{options.db.engine} \
            --databasehost=#{options.db.host} \
            --databaseport=#{options.db.port} \
            --databasename=#{options.db.database} \
            --databaseusername=#{options.db.username} \
            --databasepassword=#{options.db.password}
            # --jdbc-db=mysql \
            # --jdbc-driver=/usr/share/java/mysql-connector-java.jar \
            # --cluster-name=#{options.cluster_name} \
          [ -n "#{options.master_key}" ] && ambari-server setup-security \
            --security-option=encrypt-passwords \
            --master-key=#{options.master_key} \
            --master-key-persist=true
          """
        @call (options, callback) ->
          properties '/etc/ambari-server/conf/ambari.properties', ssh: options.ssh, (err, data) ->
            throw err if err
            for k, v of data
              props[k] ?= {}
              props[k].new = v 
            callback()
        @call (options, callback) ->
          status = false
          for k, v of props
            if v.org isnt v.new
              options.log message: "Option #{k} was #{JSON.stringify v.org} and is now #{JSON.stringify v.new}", level: 'INFO', module: 'ryba/lib/file/properties' unless v.org is v.new
              status = true
          callback null, status

## Start

Start the service or restart it if there were any changes.

      @service
        header: 'Start'
        name: 'ambari-server'
        action: ['start', 'restart']
        if: -> @status()

## Admin Credentials

      checkurl = url.format
        protocol: 'http'
        hostname: options.host
        port: options.config['client.api.port']
        pathname: '/api/v1/clusters'
      changeurl = url.format
        protocol: 'http'
        hostname: options.host
        port: options.config['client.api.port']
        pathname: '/api/v1/users/admin'
      cred = "admin:#{options.current_admin_password}"
      json = JSON.stringify "Users":
        "user_name": "admin"
        "password": "#{options.admin_password}"
        "old_password": "#{options.current_admin_password}"
      @system.execute
        header: 'Admin Credentials'
        if_exec: """
        curl -f -u #{cred} #{checkurl}
        """
        cmd: """
        curl -f -i -u #{cred} -H "X-Requested-By: ambari" -X PUT -d '#{json}' #{changeurl}
        """

## Dependencies

    url = require 'url'
    misc = require 'nikita/lib/misc'
    db = require 'nikita/lib/misc/db'
    {properties} = require 'nikita/lib/file/properties'

[sr]: http://docs.hortonworks.com/HDPDocuments/Ambari-2.2.2.0/bk_Installing_HDP_AMB/content/_meet_minimum_system_requirements.html
