
# Ambari Server Install

See the Ambari documentation relative to [Software Requirements][sr] before
executing this module.

    module.exports = header: 'Ambari Server Install', timeout: -1, handler: ->
      {ambari_server} = @config.ryba

## Identities

By default, the "ambari-server" package does not create any identities.

      @system.group header: 'Group', ambari_server.group
      @system.user header: 'User', ambari_server.user

## Package & Repository

Declare the Ambari custom repository.
Install Ambari server package.

      @file.download
        header: 'Repo'
        if: ambari_server.repo
        source: ambari_server.repo
        target: '/etc/yum.repos.d/ambari.repo'
      @system.execute
        cmd: "yum clean metadata; yum update -y"
        if: -> @status -1
      @service
        header: 'Package'
        name: 'ambari-server'
        startup: true
      @service
        header: 'Mysql Connector'
        name: 'mysql-connector-java'
        if: ambari_server.db.engine is 'mysql'

## Non-Root

      @file
        header: 'Sudo'
        if: ambari_server.sudo
        target: '/etc/sudoers.d/ambari_server'
        content: """
        # Ambari Commands
        ambari ALL=(ALL) NOPASSWD:SETENV: /bin/mkdir -p /etc/security/keytabs, /bin/chmod * /etc/security/keytabs/*.keytab, /bin/chown * /etc/security/keytabs/*.keytab, /bin/chgrp * /etc/security/keytabs/*.keytab, /bin/rm -f /etc/security/keytabs/*.keytab, /bin/cp -p -f /var/lib/ambari-server/data/tmp/* /etc/security/keytabs/*.keytab
        Defaults exempt_group = ambari
        Defaults !env_reset,env_delete-=PATH
        Defaults: ambari !requiretty
        """
      @system.remove
        header: 'Clean Sudo'
        unless: ambari_server.sudo
        target: '/etc/sudoers.d/ambari_server'

## Database

Prepare the Ambari Database

      @call header: 'Database', ->

Password is stored inside a file which location is referenced by the property
"server.jdbc.user.passwd" in the configuration file. The permissione "660" match
the ones generated by "ambari-server setup".

        @file
          header: 'Stash Password'
          unless: ambari_server.master_key
          target: ambari_server.config['server.jdbc.user.passwd']
          content: ambari_server.db.password
          backup: true
          mode: 0o0640
        # Note, for same ready, `ambari-server setup-security` keep re-generating
        # the stashed password file even if it uses the encrypted database located
        # in "/var/lib/ambari-sever/keys".
        # @system.remove
        #   header: 'Clean Stash Password'
        #   if: ambari_server.master_key
        #   target: '/etc/ambari-server/conf/password.dat'

Create the database hosting the Ambari data with restrictive user permissions.

        @db.user ambari_server.db, database: null,
          header: 'User'
          if: ambari_server.db.engine in ['mysql', 'postgres']
        @db.database ambari_server.db,
          header: 'Database'
          user: ambari_server.db.username
          if: ambari_server.db.engine in ['mysql', 'mariadb', 'postgres']
        @db.schema ambari_server.db,
          header: 'Schema'
          if: ambari_server.db.engine is 'postgres'
          schema: ambari_server.db.schema or ambari_server.db.database
          database: ambari_server.db.database
          owner: ambari_server.db.username

Load the database with initial data

        switch ambari_server.db.engine
          when 'mysql', 'mariadb'
            load = db.cmd(ambari_server.db, null) + '< /var/lib/ambari-server/resources/Ambari-DDL-MySQL-CREATE.sql'
            created = db.cmd(ambari_server.db, 'show tables') + '|  grep clusters'
          when 'postgres'
            load = db.cmd(ambari_server.db, null) + '< /var/lib/ambari-server/resources/Ambari-DDL-Postgres-CREATE.sql'
            created = db.cmd(ambari_server.db, null) + 'show tables |  grep clusters'
        @system.execute
          header: 'Init'
          cmd: load
          unless_exec: created

## Configuration

Merge used defined configuration. This could be used to set up 
LDAP or Active Directory Authentication. The permissions "644" are the ones 
generated by the "ambari-server setup" command.

      @file.properties
        header: 'Config'
        target: "#{ambari_server.conf_dir}/ambari.properties"
        content: ambari_server.config
        merge: true
        comment: true
        backup: true
        mode: 0o0644
    
## Setup

Password encryption is activated if the property "master_key" is configured. By 
default the passwords to access the Ambari database and the LDAP server are 
stored in a plain text configuration file. if Password encryption is activated, 
Ambari will store information inside "/var/lib/ambari-server/keys".

Be carefull, notes from Ambari 2.4.2:
* Options "jdbc-db" and "jdbc-driver" prevent the setup script from modifying 
  the properties file.
* Option "cluster-name" does nothing

      @call header: 'Setup', ->
        props = {}
        @call (options, callback) ->
          properties '/etc/ambari-server/conf/ambari.properties', ssh: options.ssh, (err, data) ->
            throw err if err
            for k, v of data
              props[k] ?= {}
              props[k].org = v 
            callback()
        @system.execute
          shy: true
          cmd: """
          ambari-server setup \
            -s \
            -j #{ambari_server.java_home} \
            --database=#{ambari_server.db.engine} \
            --databasehost=#{ambari_server.db.host} \
            --databaseport=#{ambari_server.db.port} \
            --databasename=#{ambari_server.db.database} \
            --databaseusername=#{ambari_server.db.username} \
            --databasepassword=#{ambari_server.db.password}
            # --jdbc-db=mysql \
            # --jdbc-driver=/usr/share/java/mysql-connector-java.jar \
            # --cluster-name=#{ambari_server.cluster_name} \
          [ -n "ambari_server.master_key" ] && ambari-server setup-security \
            --security-option=encrypt-passwords \
            --master-key=#{ambari_server.master_key} \
            --master-key-persist=true
          """
        @call (options, callback) ->
          properties '/etc/ambari-server/conf/ambari.properties', ssh: options.ssh, (err, data) ->
            throw err if err
            for k, v of data
              props[k] ?= {}
              props[k].new = v 
            callback()
        @call (options, callback) ->
          status = false
          for k, v of props
            if v.org isnt v.new
              options.log message: "Option #{k} was #{JSON.stringify v.org} and is now #{JSON.stringify v.new}", level: 'INFO', module: 'ryba/lib/file/properties' unless v.org is v.new
              status = true
          callback null, status

## Start

Start the service or restart it if there were any changes.

      @service
        header: 'Start'
        name: 'ambari-server'
        action: ['start', 'restart']
        if: -> @status()

## Admin Credentials

      checkurl = url.format
        protocol: 'http'
        hostname: @config.host
        port: ambari_server.config['client.api.port']
        pathname: '/api/v1/clusters'
      changeurl = url.format
        protocol: 'http'
        hostname: @config.host
        port: ambari_server.config['client.api.port']
        pathname: '/api/v1/users/admin'
      cred = "admin:#{ambari_server.current_admin_password}"
      json = JSON.stringify "Users":
        "user_name": "admin"
        "password": "#{ambari_server.admin_password}"
        "old_password": "#{ambari_server.current_admin_password}"
      @system.execute
        header: 'Admin Credentials'
        if_exec: """
        curl -f -u #{cred} #{checkurl}
        """
        cmd: """
        curl -f -i -u #{cred} -H "X-Requested-By: ambari" -X PUT -d '#{json}' #{changeurl}
        """

## Dependencies

    url = require 'url'
    misc = require 'nikita/lib/misc'
    db = require 'nikita/lib/misc/db'
    {properties} = require 'nikita/lib/file/properties'

[sr]: http://docs.hortonworks.com/HDPDocuments/Ambari-2.2.2.0/bk_Installing_HDP_AMB/content/_meet_minimum_system_requirements.html
