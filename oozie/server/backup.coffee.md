
# Oozie Server Backup

    module.exports = []
    module.exports.push 'masson/bootstrap'
    # module.exports.push require('./index').configure

## Database

Note: to backup the oozie database in oozie, we must add the "hex-blob" option or
we get an error while importing data. The mysqldump command does not escape all
charactere and the xml stored inside the database create syntax issues. Here's
an example:

```bash
mysqldump -uroot -ppassword --hex-blob oozie > /data/1/oozie.sql
```

    module.exports.push header: 'Oozie Server # Backup Database', timeout: -1, label_true: 'BACKUPED', handler: ->
      {db_admin, oozie} = @config.ryba
      {engine, db, hostname, port} = parse_jdbc oozie.site['oozie.service.JPAService.jdbc.url']
      user = oozie.site['oozie.service.JPAService.jdbc.username']
      password = oozie.site['oozie.service.JPAService.jdbc.password']
      engines_cmd =
        mysql: "mysqldump -u#{user} -p#{password} --hex-blob -h#{hostname} -P#{port} #{db}"
      throw Error 'Database engine not supported' unless engines_cmd[engine]
      @backup
        name: 'oozie-db'
        cmd: engines_cmd[engine]


## Logs

Archive the logs generated by Oozie Server.

    module.exports.push header: 'Oozie Server # Backup Logs', label_true: 'BACKUPED', handler: ->
      {oozie} = @config.ryba
      @backup
        name: 'oozie-logs'
        source: oozie.log_dir


## Configuration

Backup the active Oozie configuration.

    module.exports.push header: 'Oozie Server # Backup Configuration', label_true: 'BACKUPED', handler: ->
      {oozie} = @config.ryba
      @backup
        name: 'oozie-conf'
        source: oozie.conf_dir

## Dependencies

    parse_jdbc = require '../../lib/parse_jdbc'
