
# Oozie Server Backup

    module.exports = header: 'Oozie Server Backup', timeout: -1, label_true: 'BACKUPED', handler: ->
      {db_admin, oozie} = @config.ryba
## Database

Note: to backup the oozie database in oozie, we must add the "hex-blob" option or
we get an error while importing data. The mysqldump command does not escape all
charactere and the xml stored inside the database create syntax issues. Here's
an example:

```bash
mysqldump -uroot -ppassword --hex-blob oozie > /data/1/oozie.sql
```

      @call header: 'Backup Database', timeout: -1, label_true: 'BACKUPED', handler: ->
        {engine, addresses, db} = parse_jdbc oozie.site['oozie.service.JPAService.jdbc.url']
        user = oozie.site['oozie.service.JPAService.jdbc.username']
        password = oozie.site['oozie.service.JPAService.jdbc.password']
        engines_cmd =
          mysql: "mysqldump -u#{user} -p#{password} --hex-blob -h#{addresses[0].host} -P#{addresses[0].port} #{db}"
        throw Error 'Database engine not supported' unless engines_cmd[engine]
        @backup
          name: 'oozie-db'
          cmd: engines_cmd[engine]


## Logs

Archive the logs generated by Oozie Server.

      @backup
        header: 'Backup Logs'
        label_true: 'BACKUPED'
        name: 'oozie-logs'
        source: oozie.log_dir


## Configuration

Backup the active Oozie configuration.

      @backup
        header: 'Backup Configuration'
        label_true: 'BACKUPED'
        name: 'oozie-conf'
        source: oozie.conf_dir

## Dependencies

    parse_jdbc = require '../../lib/parse_jdbc'
