
# Oozie Server Backup

    module.exports = []
    module.exports.push 'masson/bootstrap/'
    module.exports.push require('./server').configure

## Database

Note: to backup the oozie database in oozie, we must add the "hex-blob" option or 
we get an error while importing data. The mysqldump command does not escape all
charactere and the xml stored inside the database create syntax issues. Here's
an example:

```bash
mysqldump -uroot -ppassword --hex-blob oozie > /data/1/oozie.sql
```

    module.exports.push name: "Oozie Server # Backup Database", timeout: -1, label_true: 'BACKUPED', handler: (ctx, next) ->
      {db_admin, oozie} = ctx.config.ryba
      {engine, db, hostname, port} = parse_jdbc oozie.site['oozie.service.JPAService.jdbc.url']
      user = oozie.site['oozie.service.JPAService.jdbc.username']
      password = oozie.site['oozie.service.JPAService.jdbc.password']
      engines_cmd =
        mysql: "mysqldump -u#{user} -p#{password} --hex-blob -h#{hostname} -P#{port} #{db}"
      return next new Error 'Database engine not supported' unless engines_cmd[engine]
      db =
        name: 'db'
        cmd: engines_cmd[engine]
        destination: "/var/backups/oozie/"
        interval: month: 1
        retention: count: 2
      ctx.backup db, next


## Logs

Archive the logs generated by Oozie Server.

    module.exports.push name: "Oozie Server # Backup Logs", label_true: 'BACKUPED', handler: (ctx, next) ->
      {oozie} = ctx.config.ryba
      logs =
        name: 'logs'
        source: oozie.log_dir
        destination: "/var/backups/oozie/"
        interval: month: 1
        retention: count: 2
      ctx.backup logs, next


## Configuration

Backup the active Oozie configuration.

    module.exports.push name: "Oozie Server # Backup Configuration", label_true: 'BACKUPED', handler: (ctx, next) ->
      {oozie} = ctx.config.ryba
      conf =
        name: 'conf'
        source: oozie.conf_dir
        destination: "/var/backups/oozie/"
        interval: month: 1
        retention: count: 2
      ctx.backup conf, next

## Module Dependencies

    parse_jdbc = require '../lib/parse_jdbc'
