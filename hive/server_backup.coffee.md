
# Hive Server Backup

The backup script dump the content of the hive database as well as the logs.

    module.exports = []
    module.exports.push 'masson/bootstrap/'
    module.exports.push require('./server').configure

## Database

    module.exports.push name: "Hive Server # Backup Database", label_true: 'BACKUPED', handler: (ctx, next) ->
      {hive} = ctx.config.ryba
      user = hive.site['javax.jdo.option.ConnectionUserName']
      password = hive.site['javax.jdo.option.ConnectionPassword']
      {engine, db, hostname, port} = parse_jdbc hive.site['javax.jdo.option.ConnectionURL']
      cmd =
        mysql: "mysqldump -u#{user} -p#{password} -h#{hostname} -P#{port} #{db}"
      return next new Error 'Database engine not supported' unless cmd[engine]
      ctx.backup 
        name: 'db'
        cmd: cmd[engine]
        destination: "/var/backups/hive/"
        interval: day: 1
        retention: count: 7
      , next

## Logs

Archive the logs generated by the Hive Server2.

    module.exports.push name: "Hive Server # Backup Logs", label_true: 'BACKUPED', handler: (ctx, next) ->
      {hive} = ctx.config.ryba
      ctx.backup
        name: 'logs'
        source: hive.log_dir
        filter: "hive-server2.log.*"
        destination: "/var/backups/hive/"
        interval: day: 1
        retention: count: 7
      , next

## Configuration

Backup the active Hive configuration.

    module.exports.push name: "Hive Server # Backup Configuration", label_true: 'BACKUPED', handler: (ctx, next) ->
      {hive} = ctx.config.ryba
      ctx.backup 
        name: 'conf'
        source: hive.conf_dir
        destination: "/var/backups/hive/"
        interval: day: 1
        retention: count: 7
      , next

## Module Dependencies

    parse_jdbc = require '../lib/parse_jdbc'
