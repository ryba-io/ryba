
# Hue backup

    module.exports = []
    module.exports.push 'masson/bootstrap/'
    module.exports.push require('./index').configure

## Database

    module.exports.push name: "Hue # Backup Database", timeout: -1, label_true: 'BACKUPED', handler: (ctx, next) ->
      {engine, host, port, user, password, name} = ctx.config.ryba.hue.ini.desktop.database
      engines_cmd =
        mysql: "mysqldump -u#{user} -p#{password} -h#{host} -P#{port} #{name}"
      return next new Error 'Database engine not supported' unless engines_cmd[engine]
      data =
        name: 'db'
        cmd: engines_cmd[engine]
        destination: "/var/backups/hue/"
        interval: month: 1
        retention: count: 2
      ctx.backup data, next

## Logs

Archive the logs generated by Hue.

    module.exports.push name: "Hue # Backup Logs", label_true: 'BACKUPED', handler: (ctx, next) ->
      {hue} = ctx.config.ryba
      logs =
        name: 'logs'
        source: hue.log_dir
        destination: "/var/backups/hue/"
        interval: month: 1
        retention: count: 2
      ctx.backup logs, next

## Configuration

Backup the active Hue configuration.

    module.exports.push name: "Hue # Backup Configuration", label_true: 'BACKUPED', handler: (ctx, next) ->
      {hue} = ctx.config.ryba
      conf =
        name: 'conf'
        source: hue.conf_dir
        destination: "/var/backups/hue/"
        interval: month: 1
        retention: count: 2
      ctx.backup conf, next
    